-- Spanning tree for unconnected graphs with negation
-- Robert J. Simmons

edge₁ : !edge X Y ->> !edge Y X.
conn₁ : !edge X Y • !connected X ->> !connected Y.
tree₁ : vertex X • ¬connected X ->> !intree X • !connected X.
tree₂ : !edge X Y • !intree X • vertex Y ->> !intree Y • !tree X Y.

{== Example trace ==}
%trace *
  !edge a b • vertex a • vertex b.

{-
  d--------   f---g---h
  |       |       |   |
  a----b--e   l   i---m
  |   /     / | \
  | /     j---k---n
  c           
-}

%exec * 
  !edge a b • !edge a c • !edge a d • !edge b c • !edge b e • !edge d e •
  !edge l j • !edge j k • !edge l k • !edge l n • !edge k n •
  !edge f g • !edge g h • !edge g i • !edge i m • !edge h m •
  vertex a • vertex b • vertex c • vertex d • vertex e • 
  vertex f • vertex g • vertex h • vertex i • vertex j • 
  vertex k • vertex l • vertex m • vertex n.


